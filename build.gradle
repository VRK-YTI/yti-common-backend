plugins {
    id "idea"
    id "java"
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.33.1"
    id "pl.allegro.tech.build.axion-release" version "1.17.0"
}

scmVersion {
    versionIncrementer("incrementMinor")

    tag {
        prefix.set("v")
        versionSeparator.set("")
    }
}

project.version = scmVersion.version

repositories {
    mavenLocal()
    mavenCentral {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }
    google {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }
    jcenter {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }

    // put your artifactory parameters to $HOME/.gradle/gradle.properties
    if (project.hasProperty("artifactoryUrl")) {
        maven {
            url "$artifactoryUrl"
            credentials {
                username "$artifactoryUsername"
                password "$artifactoryPassword"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "fi.vm.yti"
            artifactId "yti-common-backend"
            version scmVersion.version

            artifact jar
        }
    }
}

artifactory {
    publish {
        defaults {
            publications("maven")
            publishArtifacts = true
            publishPom = true
        }
    }
}

sourceCompatibility = 17
targetCompatibility = 17

dependencies {
    implementation "org.springframework:spring-webflux:6.1.5"
    implementation "org.springframework:spring-webmvc:6.1.5"
    implementation "org.springframework:spring-context:6.1.5"
    implementation "org.springframework.security:spring-security-core:6.2.3"
    implementation "org.springframework.boot:spring-boot-starter-validation:3.2.5"
    implementation "io.projectreactor.netty:reactor-netty-http:1.1.15"
    implementation "org.apache.jena:jena-core:4.10.0"
    implementation "org.apache.jena:jena-arq:4.10.0"
    implementation "org.apache.jena:jena-rdfconnection:4.10.0"
    implementation "org.apache.jena:jena-querybuilder:4.10.0"
    implementation "jakarta.validation:jakarta.validation-api:3.0.2"
    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    implementation "org.opensearch.client:opensearch-java:2.9.1"
    implementation "org.opensearch.client:opensearch-rest-client:2.12.0"

    implementation "com.google.guava:guava:33.0.0-jre"

    implementation "fi.vm.yti:yti-spring-security:0.4.0"

    compileOnly "org.jetbrains:annotations:23.0.0"

    testImplementation platform("org.junit:junit-bom:5.9.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.mockito:mockito-core:4.8.0"
    testImplementation "org.springframework.boot:spring-boot-starter-test:3.2.4"
}

test {
    useJUnitPlatform()
}